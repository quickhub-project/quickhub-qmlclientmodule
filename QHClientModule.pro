# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
# It is part of the QuickHub framework - www.quickhub.org
# Copyright (C) 2021 by Friedemann Metzger - mail@friedemann-metzger.de


TEMPLATE = lib
TARGET = QuickHub
QT += qml quick websockets
CONFIG += qt plugin c++11
CONFIG+=sdk_no_version_check
uri = QuickHub

# Input
include(QHClientModule.pri)
DISTFILES = qmldir

!equals(_PRO_FILE_PWD_, $$OUT_PWD) {
    copy_qmldir.target = $$OUT_PWD/qmldir
    copy_qmldir.depends = $$_PRO_FILE_PWD_/qmldir
    copy_qmldir.commands = $(COPY_FILE) \"$$replace(copy_qmldir.depends, /, $$QMAKE_DIR_SEP)\" \"$$replace(copy_qmldir.target, /, $$QMAKE_DIR_SEP)\"
    QMAKE_EXTRA_TARGETS += copy_qmldir
    PRE_TARGETDEPS += $$copy_qmldir.target
}

qmldir.files = qmldir
unix {
    installPath = $$[QT_INSTALL_QML]/$$replace(uri, \\., /)
    qmldir.path = $$installPath
    target.path = $$installPath
    INSTALLS += target qmldir
}

HEADERS += \
    quickhubclient_plugin.h

SOURCES += \
    quickhubclient_plugin.cpp
